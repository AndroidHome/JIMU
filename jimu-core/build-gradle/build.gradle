import jimu.Consts

plugins {
    id 'groovy'
    id 'signing'
    id 'maven-publish'
}

def MAVEN_LOCAL_PATH = uri('../../local_repo')
def GROUP_ID = Consts.GROUP_ID //'io.github.leobert-lan'
def ARTIFACT_ID = 'jimu-build-gradle'
def VERSION_NAME = Consts.Versions.jimu_plugin
def changeLog = "支持 kotlin-1.4.x 等支持Java语法的内容"

dependencies {
    implementation 'com.android.tools.build:gradle:4.1.1'
    implementation group: 'org.javassist', name: 'javassist', version: '3.23.1-GA'
    //gradle sdk
    implementation gradleApi()
    //groovy sdk
    implementation localGroovy()
    testImplementation deps.junit

}

Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)
def ACCOUNT = properties.get("nexus_user", "")
def PASSWORD = properties.get("nexus_pwd", "")

//apply plugin: 'maven'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
afterEvaluate {
    publishing {
        publications {
            mavenAndroid(MavenPublication) {
                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION_NAME
                artifact sourcesJar
                artifact javadocJar
                artifact('build/libs/build-gradle.jar')
                // 配置 pom 文件格式
                pom {
                    groupId = GROUP_ID
                    artifactId = ARTIFACT_ID
                    version = VERSION_NAME

                    packaging = 'jar'
                    name = ARTIFACT_ID
                    description = ARTIFACT_ID
                    url = siteUrl
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'mqzhang,leobert'
                            name = 'mqzhang,leobert'
                            email = 'zhmqq0527@gmail.com,leobert.l@hotmail.com'
                        }
                    }
                    scm {
                        connection = siteUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def runtimes = new ArrayList()

                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        if (it.group != null) {
                            runtimes.add(it)
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', "runtime")
                        }
                    }
                    configurations.testImplementation.allDependencies.each {
                        if (it.group != null && !runtimes.contains(it)) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', "test")
                        }
                    }
                }
            }
        }
        // 配置远程仓库
        repositories {
            maven {
                // 发布地址，新申请的项目发布地址为：https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/
//            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username ACCOUNT
                    password PASSWORD
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenAndroid
    }
}
